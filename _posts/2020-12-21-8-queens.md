---
layout:     post
title:      使用流来优雅地处理八皇后问题
subtitle:   
date:       2020-12-21
author:     Gure
header-img: img/2020-12-21/page.png
catalog: true
tags:
    - sicp
    - scheme
---

今天又过来和大家扯淡了。

<img src="https://github.com/Gurepan/Gurepan.github.io/blob/master/img/2020-12-21/1.jpg?raw=true" alt="1" style="zoom: 50%;" />

这篇文章是好久之前学习SICP时候写的，今天一不小心翻出来，于是贴出来同大家一起讨论。

本文讨论的是算法思想里面的[回溯](https://zh.wikipedia.org/wiki/%E5%9B%9E%E6%BA%AF%E6%B3%95)的经典问题：八皇后问题。

这个问题是这样的（以下摘抄至wiki）：

> *n* 皇后问题研究的是如何将 *n* 个皇后放置在 *n*×*n* 的棋盘上，并且使皇后彼此之间不能相互攻击。
>
> 皇后的走法是：可以横直斜走，格数不限。因此要求皇后彼此之间不能相互攻击，等价于要求任何两个皇后都不能在同一行、同一列以及同一条斜线上。
>
> 结果要求找出所有可能的解。

要是以回溯的思想来做是：使用一个数组记录每行放置的皇后的列下标，依次在每一行放置一个皇后。每次新放置的皇后都不能和已经放置的皇后之间有攻击：即新放置的皇后不能和任何一个已经放置的皇后在同一列以及同一条斜线上，并更新数组中的当前行的皇后列下标。当 NN 个皇后都放置完毕，则找到一个可能的解。当找到一个可能的解之后，将数组转换成表示棋盘状态的列表，并将该棋盘状态的列表加入返回列表。

这种思路会尝试生成一颗8阶，高度为8的树。然后程序会尝试遍历这颗树。这颗树有一个特点就是，只有需要某个节点的子节点时候，子节点才会被计算出来，如果父节点已经不满足皇后摆放要求时候，就不会遍历子节点，而是跳回上一层。并继续查找下一个可用节点。

```javascript
const solveNQueens = (n) => {
  const board = new Array(n);
  for (let i = 0; i < n; i++) {     // 棋盘的初始化
    board[i] = new Array(n).fill('.');
  }
  const res = [];
  const isValid = (row, col) => {  
    for (let i = 0; i < row; i++) { // 之前的行
      for (let j = 0; j < n; j++) { // 所有的列
        if (board[i][j] == 'Q' &&   // 发现了皇后，并且和自己同列/对角线
          (j == col || i + j === row + col || i - j === row - col)) {
          return false;             // 不是合法的选择
        }
      }
    }
    return true;
  };
  const helper = (row) => {   // 放置当前行的皇后
    if (row == n) {           // 递归的出口，超出了最后一行
      const stringsBoard = board.slice(); // 拷贝一份board
      for (let i = 0; i < n; i++) {
        stringsBoard[i] = stringsBoard[i].join(''); // 将每一行拼成字符串
      }
      res.push(stringsBoard); // 推入res数组
      return;
    }
    for (let col = 0; col < n; col++) { // 枚举出所有选择
      if (isValid(row, col)) {          // 剪掉无效的选择
        board[row][col] = "Q";          // 作出选择，放置皇后
        helper(row + 1);                // 继续选择，往下递归
        board[row][col] = '.';          // 撤销当前选择
      }
    }
  };
  helper(0);  // 从第0行开始放置
  return res;
};
```

在这个过程中，我们可以看到，我们为整个计算机执行定义了一个过程，到了哪一步该做什么，做完之后又应该做什么判断……我们放在一起为其指定好了。这样也带来一个问题，所有的流程都混杂在一起，我们一般来说，都必须从主流程进来，一步步看，才能明白这段代码究竟做了什么。

原本我以为对于这个问题终极解法就是这样的，直到我学习了`SICP`这门课程以后，发现其实还有另外一种写法：`Stream`

>  `Stream`是一种非常巧妙的想法，使我们尽可能利用各种序列操作，但又不会带来将序列作为表去操作而引起的代价，利用流结构，我们能得到这两个世界里最好的东西：如此形成的程序可以像序列操作那么优雅，同时又能得到递增计算的效率。

为了达成这个目的，我们要区分了解程序中书写的顺序和实际执行的顺序，并且放弃一部分对时间的控制，来获得**逻辑和流程的分离**。

用代码写的例子如下，可见，我们只需要构建最基本的算子，再在流里声明如何将这些基本的算子进行组合。就可以获得一个在抽象上定义的数据（或者说过程）。而需要的结果，将由`Stream`自身来组织运行。

```scheme
(define (calc-queen-s n)
    (if (= n 0)
        ;生成第一行的序列
        (map-stream (lambda (i) (list (cons i n))) (takes 8 int))
        ;生成过滤后的序列
        (filter-stream 
         	;传入过滤方法
            (lambda (l)
                (and (is-safe? l horizontal-attack)
                     (is-safe? l vertical-attack)
                     (is-safe? l oblique-attack))
                )
         	;传入第N行的生成方法
            (flatmap-stream 
                (lambda (e) 
                        (map-stream (lambda (i) (cons (cons i n) e)) (takes 8 int)))
                (calc-queen-s (- n 1))))
    )
    )
```

其中，`horizontal-attack`,`vertical-attack`,`oblique-attack`分别判别一个点落在棋盘上是否会受到横，竖，斜方向的攻击。而`is-safe`方法设计出来是为了验证棋盘中的棋子落法在传入的攻击方式下是否是安全的。源码如下：

```scheme
(define (horizontal-attack p1 p2)
    (= (car p1) (car p2))
    )
(define (vertical-attack p1 p2)
    (= (cdr p1) (cdr p2))
    )
(define (oblique-attack p1 p2)
    (= 
        (abs (- (cdr p1) (cdr p2)))
        (abs (- (car p1) (car p2))))
    )
(define (is-safe? l pred)
    (define (is-safe-inline p l pred)
        (if (eqv? l '())
            #t
            (if (pred p (car l))
                #f
                (is-safe-inline p (cdr l) pred)
            )
        )
        )
    (is-safe-inline (car l) (cdr l) pred)
    )
```

只需要定义最基本的规则即可，流程由`Stream`来组装。

为了让一下对`Stream`不太了解的小伙伴理解其API，这里也把API的源码贴上来，其实特别简单：

```scheme
(define (take n s)
    (if (<= n 1)
        (head s)
        (take (- n 1) (tail s))
        )
    )
(define (add-s s1 s2)
    (cond ((empty-stream? s1) s2)
          ((empty-stream? s2) s1)
          (else (cons-stream 
                             (+ (head s1) (head s2))
                             (add-s (tail s1) (tail s2))
                             ))
          )
    )
(define (muti-s s1 s2)
    (cond ((empty-stream? s1) s2)
          ((empty-stream? s2) s1)
          (else (cons-stream 
                             (* (head s1) (head s2))
                             (muti-s (tail s1) (tail s2))
                             ))
          )
    )
(define the-empty-stream '())
(define (empty-stream? s)
    (eqv? s the-empty-stream)
    )
(define (takes n s)
    (if (<= n 0)
        the-empty-stream
        (cons-stream (head s) (takes (- n 1) (tail s)))
        )
    )
(define (print-stream s)
    (if (empty-stream? s)
        ()
        (begin
            (display (head s))
            (newline)
            (print-stream (tail s))
        )
    )
    )
(define (map-stream proc s)
    (if (empty-stream? s)
        s
        (cons-stream 
                    (proc (head s))
                    (map-stream proc (tail s))
                    )
    )
    )
(define (append-stream s1 s2)
    (if (empty-stream? s1)
        s2
        (cons-stream 
                    (head s1)
                    (append-stream (tail s1) s2))
        ))
(define (accu-stream porc s init)
    (if (empty-stream? s)
        init
        (accu-stream porc (tail s) (porc init (head s)))
    )
    )
(define (flatten-stream s)
    (accu-stream append-stream (tail s) (head s))
    )
(define (flatmap-stream proc s)
    (flatten-stream (map-stream proc s))
    )
(define (filter-stream pred s)
    (cond ((empty-stream? s) the-empty-stream)
          ((pred (head s)) (cons-stream (head s) (filter-stream pred (tail s))))
          (else (filter-stream pred (tail s)))
     )
    )
```

写完以后我们可以对其结果进行打印，用以下代码：

```scheme
(display (accu-stream (lambda (x y)(+ x 1)) (calc-queen-s 7) 0))
(newline)
(print-stream (calc-queen-s 7))
```

结果输出如下：

![result](https://github.com/Gurepan/Gurepan.github.io/blob/master/img/2020-12-21/res.jpg?raw=true)

因为每个流生成时候都经过`filter-stream`进行过滤，可以先前描述好定义，只计算所需的节点，只有需要的时候，新的数据才会生成。所以整个计算时间复杂度和回溯算法是一样的，都是*O*(*n*2)。

说到这里也顺路提及一下邱奇计数和皮亚诺公理的内容吧，邱奇声称，任何“可计算”的运算，都可以写成一个λ表达式。可推理得一个集合也可以是一个λ表达式，因此，自然数序列自然也是λ表达式了。

皮亚诺公理系统由五条基于自然数集`N`和后继函数`S`的公理组成：

> - 0∈N
> -  x∈N→S(x)∈N
> -  x∈N→S(x)≠0
> -  x∈N∧y∈N∧S(x)=S(y)→x=y
> -  0∈M∧∀x(x∈M→S(x)∈M)→N⊆M for any property M (axiom of induction).

从上面可以看出，只要拥有`起始元素`（如0）以及后续元素生成方式(如`+1`)就可以推演出整个自然数域，这与流的思想不谋而合。因此，可以用`Stream`来表示自然数。

```scheme
(define one (cons-stream 1 one))
(define int 
    (cons-stream 0 
                 (add-s one int)))
```

同样的，也可以推演出`Fibonacci`域：

```scheme
(define fibs 
    (cons-stream 0 
                 (cons-stream 1 
                              (add-s fibs (tail fibs)))))
```

同上，我们现在也已经应该了解：我们上述求解的八皇后问题，事实上推演出了整个的`N皇后域`并取出了第八个元素。

在最后再做一个总结吧。这篇文章大家也可以看出来，主要不是在讲八皇后问题，而是在讨论流。这篇文章一直在有意识地诱导大家，一步步地放弃对程序中每一行代码、每一个数据的精确控制权，以此来释放隐藏在程序中的惊人表达力。甚至在最后，我们严重地混淆了**数据和过程之间的关系**，由此来打破大家对程序认知的鸿沟，希望让大家理解到，**我们每天处理的数据，编写的程序，从另一个角度来看究竟是什么，我们整天在做什么**。

到这里文章已经结束了，作为彩蛋，给大家看一个我认为最优雅八皇后代码：

![erlang](https://github.com/Gurepan/Gurepan.github.io/blob/master/img/2020-12-21/erlang.png?raw=true)

